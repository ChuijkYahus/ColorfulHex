plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.9.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	flatDir {
		dir 'libs'
	}
	maven{
		name = "Blamejared"
		url ="https://maven.blamejared.com/"
	}
	maven{
		name = "Architectury"
		url ="https://maven.architectury.dev/"
	}
	maven{
		name = "JamesWhiteShirt"
		url ="https://maven.jamieswhiteshirt.com/libs-release/"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
	}
	maven {
		name = "Terraformermc"
		url = "https://maven.terraformersmc.com/releases"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
	maven {
		name = "Jitpack"
		url = 'https://jitpack.io'
	}
	maven{
		name = "Devos"
		url = "https://mvn.devos.one/snapshots/"
	}
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
	maven { url "https://maven.shedaniel.me" }
	mavenCentral()
}

loom {
    splitEnvironmentSourceSets()

	mods {
		"hexpigmentplus" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation("net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}")

	// Hexcasting
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:$clothConfigVersion") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:$cardinalComponentsVersion"
	modImplementation "at.petra-k.paucal:paucal-fabric-$minecraft_version:$paucalVersion"
	modImplementation "vazkii.patchouli:Patchouli:$minecraft_version-$patchouliVersion-FABRIC"
	modRuntimeOnly "jempasam.colorful_hex:serialization-hooks:0.4.99999"
	modImplementation("at.petra-k.hexcasting:hexcasting-fabric-$minecraft_version:$hexcastingVersion"){
		exclude module: "phosphor"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 17
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories { }
}